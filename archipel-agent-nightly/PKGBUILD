# Maintainer: Damian Nowak <spam@nowaker.net>
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Shanto <shanto@hotmail.com>

pkgname='archipel-agent-nightly'
pkgver='20131207'
pkgrel='1'
pkgdesc='A solution to manage and supervise virtual machines. (Agent)'
arch=('any')
url='http://archipelproject.org/'
license=('AGPL')
depends=(
  'python2' 'python2-apscheduler' 'python2-sqlalchemy'
  'python2-numpy' 'python2-magic' 'python2-ctypes'
  'python-imaging' 'xmpppy' 'dnsmasq'
  'libvirt' 'libvirt-python' 'qemu'
)
makedepends=('python2-distribute')
optdepends=('ejabberd: An XMPP Server used by archipel for communication with VMs')
provides=('archipel-agent')
conflicts=('archipel-agent')
options=(!strip !emptydirs)
backup=('etc/archipel/archipel.conf')
install='archipel-agent.install'
source=('http://nightlies.archipelproject.org/latest-archipel-agent.tar.gz'
        'initinstall.patch'
        'archipel.service')
sha512sums=('SKIP'
            '8179b152874dcb6936d5c556a7f8cab3325365fdb171b34b96b365deab6e7b51601f8d96d7bdf8d7cb600a993ba4726e91f23c6f2683154a3613116ceb361339'
            '64eb6ea8f8890a0d98eb1dc447c235d5db3fcb4fb74b9e3c29e2aa5f6d1fcc5e7156a78bbaabe99778e2b489b0508c1dbfc089bb83c997dbf1a6dc70904cbcf0')

package() {
  cd "$srcdir"

  cd `find "$srcdir" -type d -name ArchipelAgent -print -quit`

  for mod in archipel-core archipel-agent-* archipel-agent; do
    pushd $mod 1>/dev/null
    if [ -f 'setup.py' ]; then
      python2 setup.py install --root="$pkgdir/" --optimize=1
    fi
    popd 1>/dev/null
  done;

  mkdir -p "$pkgdir/etc/systemd"
  mkdir -p "$pkgdir/usr/lib/systemd/system"

  cd "$pkgdir"
  patch -p1 < "$srcdir"/initinstall.patch
  "$pkgdir"/usr/bin/archipel-initinstall -p "$pkgdir" > /dev/null 2>&1

  cp "$pkgdir"/usr/install/usr/lib/systemd/system/archipel-agent.service "$pkgdir"/usr/lib/systemd/system/archipel.service
  rm -rf "$pkgdir/usr/install"
}
