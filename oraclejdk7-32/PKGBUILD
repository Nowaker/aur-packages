# Maintainer: Damian Nowak <damian.nowak@atlashost.eu>

_major='7'
_minor='67'
_build='b01'
_bit='32'
_arch='i586'
pkgname="oraclejdk${_major}-${_bit}"
pkgver="${_major}u${_minor}"
pkgrel='1'
pkgdesc="Non-conflicting ${_bit}-bit Oracle JDK ${_major} for servers"
arch=('i686' 'x86_64')
url='http://www.oracle.com/technetwork/java/javase/downloads/jdk7-downloads-1880260.html'
license=('unknown')
depends=('lib32-glibc' 'lib32-libjpeg-turbo' 'lcms2' 'lib32-nss' 'ca-certificates-java')
# TODO: Add dependency on lib32-lcms2 once they add it to repo. https://bugs.archlinux.org/task/32994
[ "$CARCH" == 'i686' ] && depends=('libjpeg-turbo' 'lcms2' 'nss' 'ca-certificates-java')
provides=("java-environment=${_major}" "java-runtime=${_major}" "java-runtime-headless=${_major}" "java-web-start=${_major}")
DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -O -b "oraclelicense=a"')
source=("http://download.oracle.com/otn-pub/java/jdk/${pkgver}-${_build}/jdk-${pkgver}-linux-${_arch}.tar.gz")
sha512sums=('18ae866d528dca8ce0dc34bc8c84194222ad4bdc415d77e31fe5e9637274af00713ca48701db3f92649ad6a6dec708e4a1b1ee19e329c880d524a1b1f7f9f851')
options=(!strip)
PKGEXT=('.pkg.tar.gz')

package() {
  cd "$srcdir"
  install -dm755 "$pkgdir/usr/lib/jvm" "$pkgdir/etc/conf.d"
  mv "jdk1.7.0_${_minor}" "$pkgdir/usr/lib/jvm/$pkgname"
  echo "JAVA_HOME=\"/usr/lib/jvm/$pkgname\"" > "$pkgdir/etc/conf.d/$pkgname"
}

