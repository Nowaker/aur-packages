# Maintainer: Damian Nowak <damian.nowak@atlashost.eu>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>
# Contributor: Ernie Brodeur <ebrodeur@ujami.net>
# Contributor: Rogof <fake.bios at gmail>
# Contributor: m4.rc0 <m4.rc0 at o2 (dot) pl>
pkgname='rubymine-eap'
_pkgname='RubyMine'
pkgver='6.0_132.104'
_pkgver='132.104'
pkgrel='1'
pkgdesc="Ruby and Rails IDE with the full stack of essential developer tools."
arch=('i686' 'x86_64')
options=('!strip')
url="http://www.jetbrains.com/rubymine"
license=('custom')
depends=('java-runtime')
source=(http://download.jetbrains.com/ruby/${_pkgname}-${_pkgver}.tar.gz
        jetbrains-rubymine
        jetbrains-rubymine.desktop)
sha256sums=('614a2a87d05a398528d6b1533a9c183b584946fdde8dfa29daa5be33abbce649'
            '5907872548a4698c4a58a229296ff519031fba30b070257ff1a5e308faaff3c3'
            '8120624912912c0c8edfcde9705ae38a395007caccfb499a39ef6c172b2793f4')
PKGEXT='.tar.gz' # prevent a time-consuming compression with xz

package() {
  cd "${srcdir}"
  mkdir -p "${pkgdir}/opt/${pkgname}"
  cp -r ${srcdir}/${_pkgname}-${_pkgver}/* "${pkgdir}/opt/${pkgname}"
  if [[ $CARCH = 'i686' ]]; then
    rm -f "${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux64.so"
    rm -f "${pkgdir}/opt/${pkgname}/bin/libbreakgen64.so"
    rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier64"
  fi
  if [[ $CARCH = 'x86_64' ]]; then
    rm -f "${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux.so"
    rm -f "${pkgdir}/opt/${pkgname}/bin/libbreakgen.so"
    rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier"
  fi

  mkdir -p "${pkgdir}/usr/bin"
  mkdir -p "${pkgdir}/usr/share/applications"
  mkdir -p "${pkgdir}/usr/share/pixmaps"
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "${startdir}/jetbrains-rubymine.desktop" "${pkgdir}/usr/share/applications/jetbrains-rubymine-eap.desktop"
  install -m 644 "${pkgdir}/opt/${pkgname}/bin/RMlogo.svg" "${pkgdir}/usr/share/pixmaps/${pkgname}.svg"
  install -m 644 "${srcdir}/${_pkgname}-${_pkgver}/license/${_pkgname}_license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/${_pkgname}_license.txt"
  install -m 755 "${startdir}/jetbrains-rubymine" "${pkgdir}/usr/bin/jetbrains-rubymine-eap"
}

