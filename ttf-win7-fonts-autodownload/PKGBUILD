# Maintainer: Damian Nowak <damian.nowak@atlashost.eu>
# Contributor: Martin Schm√∂lzer <mschmoelzer@gmail.com>

# Fork of https://aur.archlinux.org/packages/ttf-win7-fonts/
pkgname='ttf-win7-fonts-autodownload'
pkgver=7.1
pkgrel=1
pkgdesc="Microsoft Windows 7 TrueType Fonts (autodownloaded from Microsoft)"
arch=('any')
url="http://www.microsoft.com"
license=('custom')
depends=('fontconfig')
makedepends=('fontforge' 'cabextract')
provides=('ttf-vista-fonts' 'ttf-ms-fonts' 'ttf-win7-fonts')
conflicts=('ttf-vista-fonts' 'ttf-ms-fonts' 'ttf-win7-fonts')
install=$pkgname.install
source=('http://download.microsoft.com/download/0/A/F/0AFB5316-3062-494A-AB78-7FB0D4461357/windows6.1-KB976932-X64.exe'
        'license.rtf')
sha256sums=('f4d1d418d91b1619688a482680ee032ffd2b65e420c6d2eaecf8aa3762aa64c8'
            '096cdd18e26b2b3d8cf0b3ec6a1ffb5b0eaae0fcc2105e3c145f19037a42d467')

build() {
  cd "${srcdir}"

  cabextract -F '*.tt[fc]' -d 'win7_sp1' 'windows6.1-KB976932-X64.exe'
  find 'win7_sp1' -name *.tt[fc] -exec cp '{}' . \;

  # cambria.ttc is a TrueType Collection (TTC) which causes problems with
  # LibreOffice (Cambria Reguar cannot be selected) so we need to convert
  # the TTC into a TrueType Font (TTF).
  FONTFORGE_LANGUAGE=ff fontforge -c 'Open("cambria.ttc(Cambria)"); Generate("cambria.ttf")'
}

package() {
  cd "${srcdir}"

  # Prepare destination directory
  install -d -m 755 "${pkgdir}/usr/share/fonts/TTF"

  # Install fonts and license
  install -m 644 *.ttf "${pkgdir}/usr/share/fonts/TTF"
  install -D -m 644 "license.rtf" "${pkgdir}/usr/share/licenses/${pkgname}/license.rtf"
}

# vim: set ts=2 sw=2 ft=sh noet:

