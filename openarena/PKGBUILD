# Maintainer: Damian Nowak <damian.nowak@atlashost.eu>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname='openarena'
pkgver='0.8.8'
_pkgver='088'
pkgrel='2'
pkgdesc="A violent, sexy, multiplayer first person shooter based on the ioquake3 engine (binary package)"
arch=('i686' 'x86_64')
url="http://openarena.ws/"
license=('GPL')
depends=('sdl' 'libvorbis' 'curl') 
source=("http://download.tuxfamily.org/openarena/rel/${_pkgver}/openarena-${pkgver}.zip"
        'openarena-runner.sh'
        'openarena.png'
        'openarena-server.png'
        'openarena.desktop'
        'openarena-server.desktop')
sha512sums=('9fa4dabe8a3428dc3cbec97f3b4d20c04569c14cdd00b60e6391c6dd61e310f246ff5ec97e7549821b3d6f5f94b140eb5411a2ddd83dafcad66937b7f78ea8dd'
            'de231ee0cedfd23d4f00ffecee8912e53a761522aea20c34296d438bf3037819ae934251790dcba692e6b7a13ea47e9ca4cf5acfb3c0292334633ca10430a70a'
            'ef68f8eb6251c3424464702ff894a6b88b473a3f4c1512af613125f5e5a7124f268490a9f6042095ff5bb807817e1f302c80d21987a2ed178e680f993d70b6f1'
            '9e8c860fe8e9a6fefa9839907df6e74f11564daaa2bf12b4c261dec34d65917e7110a41aa7777f16d3842c8371b9e960468d75c439d8b815454feeb8c01ed6e6'
            'cc0192a48db6779b6d2c25d9ce49b06d113a549be43461eb8cf0f02e8511a5f257e2a9bc9615dda7b109e9df4d8b062869d6483fdb1cc7a0044d8f2ae37d20cf'
            '302769dc988fff7f31b0484052e32d8a818f0b1f89bcaf7adc39c2785da1a60c6565520dd24f6b1965eb6b21e98b1f9805eada3e5678a78d6ed008b3c1426b63')
PKGEXT=".pkg.tar.gz"

build() {
  cd "${srcdir}/openarena-${pkgver}"
  rm *.dll *.exe
  rm -rf __MACOSX *.app
}

package() {
  output="${pkgdir}/opt"
  install -dm755 "${output}" "$pkgdir"/usr/share/pixmaps/ "$pkgdir"/usr/share/applications/ "$pkgdir"/usr/bin
  mv "${srcdir}/openarena-${pkgver}" "${pkgdir}/opt/openarena"
  find "${pkgdir}/opt/openarena" -type f -exec chmod 644 {} \;
  find "${pkgdir}/opt/openarena" -type d -exec chmod 755 {} \;
  chmod 755 "${pkgdir}"/opt/openarena/*.{i386,x86_64}

  install -Dm 644 "$srcdir"/openarena{,-server}.png "$pkgdir"/usr/share/pixmaps/
  install -Dm 644 "$srcdir"/openarena{,-server}.desktop "$pkgdir"/usr/share/applications/

  install -Dm 755 "$srcdir"/openarena-runner.sh "$pkgdir"/opt/openarena/openarena-runner.sh
  ln -s /opt/openarena/openarena-runner.sh "$pkgdir"/usr/bin/openarena
  ln -s /opt/openarena/openarena-runner.sh "$pkgdir"/usr/bin/openarena-server
}
