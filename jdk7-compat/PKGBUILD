# Maintainer: Damian Nowak <damian.nowak@atlashost.eu>
# Contributor: dreieck
# Contributor: Det
# Based on jdk: <https://aur.archlinux.org/packages/jdk/>, which in turn is based on jre: <https://aur.archlinux.org/packages.php?ID=51908>

pkgname=jdk7-compat
_major=7
_minor=45
_build=b18
_pkg="${_major}u${_minor}"
pkgver="${_major}.${_minor}"
pkgrel=1
pkgdesc="The Java Development Kit, designed to be installed in parallel with another java installation."
url=http://www.oracle.com/technetwork/java/javase/downloads/jdk7-downloads-1880260.html
arch=('i686' 'x86_64')
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libx11' 'libxrender' 'libxtst' 'shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: sound support'
            'ttf-dejavu: fonts')
license=('custom')
# backup=('etc/conf.d/derby-network-server'
#         'etc/profile.d/jdk.csh'
#         'etc/profile.d/jdk.sh')
install=jdk.install
provides=("java-web-start=${pkgver}" "jdk=${pkgver}" "jre7-compat=${pkgver}" "jre=${pkgver}")
replaces=("jre7-compat")
conflicts=("jre7-compat")

_arch=i586  # Workaround for the AUR Web interface Source parser
_arch2=i386; [ "$CARCH" = 'x86_64' ] && _arch=x64 && _arch2=amd64

source=("http://download.oracle.com/otn-pub/java/jdk/${_pkg}-${_build}/jdk-${_pkg}-linux-${_arch}.tar.gz"
        'derby-network-server'
        'derby-network-server.conf'
        'java-monitoring-and-management-console.desktop'
        'java-policy-settings.desktop'
        'java-visualvm.desktop'
        'javaws-launcher'
        'jdk.csh'
        'jdk.sh'
        'jdk.install')

md5sums=('66b47e77d963c5dd652f0c5d3b03cb52'  # jdk-$_pkg-linux-i586.tar.gz
         'a279e195e249000646895d93e199860d'  # derby-network-server
         '4bdff6982c66d24a879c424aaac3d04d'  # derby-network-server.conf
         'da10de5e6507c392fc9871076ef53ec6'  # java-monitoring-and-management-console.desktop
         'f4e25ef1ccef8d36ff2433c3987a64fe'  # java-policy-settings.desktop
         '35fd89c5c170021d2183593335703703'  # java-visualvm.desktop
         '45c15a6b4767288f2f745598455ea2bf'  # javaws-launcher
         'e81bb7853b071df6adca0b90f0c2ac2d'  # jdk.csh
         'a06d46b277e2926d73d2ee11c9950973'  # jdk.sh
         '2489adaa2a18192a1c46665adfffa319') # jdk.install

[ "$CARCH" = 'x86_64' ] && md5sums[0]='bea330fcbcff77d31878f21753e09b30'  # 64bit-version.

### Need to set cookie, otherwise the oracle server won't provide the file.
DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -O --header "Cookie: gpw_e24=h"')

PKGEXT=".pkg.tar"


package() {
  msg2 "Creating required dirs"
  
  installdir="/opt/java${_major}"
  destdir="${pkgdir}/${installdir}"
  
  cd "jdk1.${_major}.0_${_minor}"
  
  mkdir -p "${destdir}"
  mkdir -p "${destdir}"/usr/share
  mkdir -p "${destdir}"/etc/{.java/.systemPrefs}
  mkdir -p "${destdir}"/usr/lib/mozilla/plugins
  mkdir -p "${destdir}"/etc/profile.d
  mkdir -p "${destdir}"/etc/rc.d
  mkdir -p "${destdir}"/etc/conf.d
  mkdir -p "${destdir}"/usr/share/applications
  mkdir -p "${pkgdir}"/usr/share/licenses/"${pkgname}"

  msg2 "Removing the redundancies"
  rm -r db/bin/*.bat jre/{plugin/,COPYRIGHT,LICENSE,*.txt} man/ja # lib/{desktop,visualvm/platform/docs}

  msg2 "Moving stuff in place"
  mv jre/lib/desktop/* man "${destdir}"/usr/share/
  mv COPYRIGHT LICENSE *.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/
  mv * "${destdir}"

  msg2 "Symlinking the plugin"
  ln -s "${installdir}"/jre/lib/"${_arch2}"/libnpjp2.so "${destdir}"/usr/lib/mozilla/plugins/

  msg2 "Installing the scripts, confs and .desktops of our own"
  cd "${srcdir}"
  install -m755 jdk.{,c}sh "${destdir}"/etc/profile.d/
  install -Dm644 derby-network-server.conf "${destdir}"/etc/conf.d/derby-network-server
  install -m755 derby-network-server "${destdir}"/etc/rc.d/
  install -m755 javaws-launcher "${destdir}"/jre/bin/
  install -m644 *.desktop "${destdir}"/usr/share/applications/

  msg2 "Tweaking the javaws .desktop file"
  sed -e 's/Exec=javaws/&-launcher %f/' -e '/NoDisplay=true/d' -i "${destdir}"/usr/share/applications/sun-javaws.desktop
}

