# Maintainer: Damian Nowak <spam@nowaker.net>
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Shanto <shanto@hotmail.com>

pkgname='archipel-agent-git'
pkgver='20140404'
pkgrel='1'
pkgdesc='A solution to manage and supervise virtual machines. (Agent)'
arch=('any')
url='http://archipelproject.org/'
license=('AGPL')
depends=(
  'python2' 'python2-apscheduler' 'python2-sqlalchemy'
  'python2-numpy' 'python2-magic' 'python2-ctypes'
  'python-imaging' 'xmpppy' 'dnsmasq'
  'libvirt' 'libvirt-python' 'qemu'
)
makedepends=('python2-setuptools>=3.4')
optdepends=('ejabberd: An XMPP Server used by archipel for communication with VMs')
provides=('archipel-agent')
conflicts=('archipel-agent')
options=(!strip !emptydirs)
install='archipel-agent.install'
source=('git://github.com/ArchipelProject/Archipel.git'
        'initinstall.patch'
)
sha512sums=('SKIP'
            '8179b152874dcb6936d5c556a7f8cab3325365fdb171b34b96b365deab6e7b51601f8d96d7bdf8d7cb600a993ba4726e91f23c6f2683154a3613116ceb361339'
)          

package() {
  cd "$srcdir"

  cd `find "$srcdir" -type d -name ArchipelAgent -print -quit`

  for mod in archipel-*; do
    pushd $mod 1>/dev/null
    if [ -f 'setup.py' ]; then
      python2 setup.py install --root="$pkgdir/" --optimize=1
    fi
    popd 1>/dev/null
  done;

  cd "$pkgdir"
  patch -p1 --no-backup-if-mismatch < "$srcdir"/initinstall.patch
  "$pkgdir"/usr/bin/archipel-initinstall -p "$pkgdir" > /dev/null 2>&1

  install -dm0700 "$pkgdir/etc/archipel" "$pkgdir/var/lib/archipel"

  mkdir -p "$pkgdir/usr/lib/systemd/system"
  cp "$pkgdir"/usr/install/usr/lib/systemd/system/*.service "$pkgdir"/usr/lib/systemd/system/

  find "$pkgdir"/usr/install/etc/archipel -type f -exec mv '{}' '{}'.example \;
  mv "$pkgdir"/usr/install/etc/archipel/* "$pkgdir/etc/archipel"
  mv "$pkgdir"/usr/install/var/lib/archipel/* "$pkgdir/var/lib/archipel"
  rm -rf "$pkgdir/usr/install"
}
