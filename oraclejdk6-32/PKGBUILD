# Maintainer: Damian Nowak <damian.nowak@atlashost.eu>

_major='6'
_minor='45'
_build='b06'
_bit='32'
_arch='i586'
pkgname="oraclejdk${_major}-${_bit}"
pkgver="${_major}u${_minor}"
pkgrel='1'
pkgdesc="Non-conflicting Oracle JDK ${_major} for servers"
arch=('i686' 'x86_64')
url='http://www.oracle.com/technetwork/java/javase/downloads/java-archive-downloads-javase6-419409.html'
license=('unknown')
depends=('lib32-glibc' 'lib32-libjpeg-turbo' 'lcms2' 'lib32-nss' 'ca-certificates-java')
# TODO: Add dependency on lib32-lcms2 once they add it to repo. https://bugs.archlinux.org/task/32994
[ "$CARCH" == 'i686' ] && depends=('libjpeg-turbo' 'lcms2' 'nss' 'ca-certificates-java')
provides=("java-environment=${_major}" "java-runtime=${_major}" "java-runtime-headless=${_major}" "java-web-start=${_major}")
DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -O --header "Cookie: gpw_e24=h"')
source=("http://download.oracle.com/otn/java/jdk/${pkgver}-${_build}/jdk-${pkgver}-linux-${_arch}.bin")
sha512sums=('fe524d8b369cc26960e657db7e8bcb14c23e54f9a2078c2da49b74bf7dc8b4126d23e5dfeaddb61e549542a8f24ced93842358f6a8548b39795099b809f420f9')
options=(!strip)
PKGEXT=('.pkg.tar.gz')
echo $source

package() {
  cd "$srcdir"
  install -dm755 "$pkgdir/usr/lib/jvm" "$pkgdir/etc/conf.d"

  zip="jdk-$pkgver-linux-${_arch}.bin"
  chmod +x "$zip"
  
  (
    pid="$BASHPID"
    yes | "./$zip" 2> /dev/null | while read line; do
      [[ "$line" == 'Press Enter to continue.....' ]] && kill -9 $pid
    done
  ) || echo

  mv "jdk1.6.0_${_minor}" "$pkgdir/usr/lib/jvm/$pkgname"
  echo "JAVA_HOME=\"/usr/lib/jvm/$pkgname\"" > "$pkgdir/etc/conf.d/$pkgname"
}
