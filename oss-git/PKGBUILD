# Maintainer: Damian Nowak <damian.nowak@atlashost.eu>
# Maintainer: Sebastian Schwarz <seschwar@gmail.com>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=oss-git
pkgname=oss-git
true && pkgname=(oss-git libflashsupport-oss)
pkgver=20130928
pkgrel=1
arch=(i686 x86_64)
url="http://developer.opensound.com/"
license=(GPL2)
makedepends=(gcc47 gtk2)
source=(oss::git://git.code.sourceforge.net/p/opensound/git
        oss.service
        remove-hal.patch
        rm-init-scripts.patch
        soundon.patch)
md5sums=(SKIP
         '39c46b10a6bc56fc146d669a61bd3028'
         'cd7f1dc6166bba8c94d96f3a28e948a5'
         'b9a380a0ac8896390d71ac13676f27e1'
         '65f07fe241bfbf912f76d8b6d8f276b5')

pkgver() {
  cd "$srcdir/oss"
  git describe --always | sed 's/-/./g'
}

prepare() {
  cd "$srcdir/oss"

  # OSS wants an empty build directory
  rm -rf build
  mkdir build

  # remove outdated stuff
  cd setup/Linux
  patch -p2 < "$srcdir/rm-init-scripts.patch"
  rm oss/etc/S89oss
  patch -p2 < "$srcdir/remove-hal.patch"
  rm oss/scripts/*oss_usb-create-device*
  patch -p1 < "$srcdir/soundon.patch"
  cd ../..
}

build() {
  cd "$srcdir/oss/build"
  ../configure --enable-libsalsa=NO --regparm
  make CC=gcc-4.7 build
  gcc-4.7 $CFLAGS -shared -fPIC -Wall -Werror oss/lib/flashsupport.c \
      -o libflashsupport.so
}

package_libflashsupport-oss() {
  pkgdesc="Adobe flash plugin support lib (OSSv4)"
  conflicts=(libflashsupport libflashsupport-pulse)
  depends=(oss)

  install -dm755 "$pkgdir/usr/lib"
  ln -s oss/lib/libflashsupport.so "$pkgdir/usr/lib/libflashsupport.so"
}

package_oss-git() {
  pkgdesc="Open Sound System UNIX audio architecture"
  depends=(gcc make linux-headers libtool sed)
  optdepends=('gtk2: for graphical mixer (ossxmix)'
              'libflashsupport-oss: Adobe flash plugin support')
  provides=(oss)
  conflicts=(oss oss-linux oss-linux-free oss-testing)
  backup=(usr/lib/oss/soundon.user)
  install=oss.install

  cd "$srcdir/oss/build"
  make DESTDIR="$pkgdir/" copy
  install -Dm755 libflashsupport.so \
      "$pkgdir/usr/lib/oss/lib/libflashsupport.so"
  cd "$pkgdir"

  # usr-move fixes
  mv usr/sbin/* usr/bin
  rmdir usr/sbin
  grep -IlrZ '\<s\?bin\>' . \
      | xargs -0 sed -i 's,\<\(usr/\)\?s\?bin\>,usr/bin,g' --
  grep -IlrZ '\<lib/modules\>' . \
      | xargs -0 sed -i 's,\<\(usr/\)\?lib/modules\>,usr/&,g' --

  # make OSS install its modules to /usr/lib/modules/$KERNEL/extramodules/oss
  grep -IlrZ '\<usr/lib/modules/[^/]\+/kernel/oss\>' . \
      | xargs -0 sed -i 's,\<usr/lib/modules/\([^/]\+\)/kernel/oss\>,usr/lib/modules/\1/extramodules/oss,g' --

  chmod -R a+rX .  # FS#13815
  install -Dm644 "$srcdir/oss.service" usr/lib/systemd/system/oss.service
}
