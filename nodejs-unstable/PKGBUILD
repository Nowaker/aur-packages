# Maintainer: Damian Nowak <damian.nowak@pacmanvps.com>
# Contributor: Xia Biao <423300@gmail.com>
pkgname='nodejs-unstable'
pkgver='0.11.16'
pkgrel='1'
pkgdesc='Evented I/O for V8 javascript'
arch=('i686' 'x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('openssl')
makedepends=('python2')
checkdepends=('curl')
provides=('nodejs')
conflicts=('nodejs')
options=('!emptydirs')
source=("http://nodejs.org/dist/v$pkgver/node-v$pkgver.tar.gz")
sha256sums=('f0d141faa1f7da3aff53e9615d76040d29c0650542be3b09ee80aca2f2cc61f6')

prepare() {
  cd "node-v$pkgver"

  msg 'Fixing for python2 name'
  find -type f -exec sed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e "s_'python'_'python2'_" -i {} \;
  find test/ -type f -exec sed 's_python _python2 _' -i {} \;
}

build() {
  cd "node-v$pkgver"

  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --shared-openssl

  make
}

check() {
  cd "node-v$pkgver"
  make test || true
}

package() {
  cd "node-v$pkgver"

  make DESTDIR="$pkgdir" install

  # install docs as per user request
  install -d "$pkgdir"/usr/share/doc/nodejs
  cp -r doc/api/{*.html,assets} \
    "$pkgdir"/usr/share/doc/nodejs

  install -D -m644 LICENSE \
    "$pkgdir"/usr/share/licenses/nodejs/LICENSE
}
